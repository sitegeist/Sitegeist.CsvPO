
prototype(Sitegeist.CsvPO:Views.UpdateOverride) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Update override for translation of "{translationOverride.labelIdentifier}" in {source.name}</legend>
        <p>{source.identifier}</p><br/>

        <Neos.Fusion.Form:Form
            form.target.action="saveOverride"
            form.data.translationOverride={translationOverride}
        >
            <Neos.Fusion.Form:Hidden field.name="translationOverride[sourceIdentifier]" />
            <Neos.Fusion.Form:Hidden field.name="translationOverride[labelIdentifier]" />
            <Neos.Fusion.Form:Hidden field.name="translationOverride[localeIdentifier]" />

            <div class="neos-control-group">
                <label class="neos-control-label" for="translation">Original translation</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input attributes.value={translation.translation} attributes.class="neos-span12" attributes.readonly />
                </div>
            </div>

            <div class="neos-control-group">
                <label class="neos-control-label" for="translation">Translation</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Textarea field.name="translationOverride[translation]" attributes.class="neos-span12" />
                </div>
            </div>

            <div class="neos-footer">
                <button class="neos-button neos-button-danger" title="delete overrode" data-toggle="modal" href="#deleteOverride">
                    Delete override
                </button>
                <Neos.Fusion:Link.Action href.action="show" class="neos-button" href.arguments.sourceIdentifier={source.identifier}>Back</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Save Override</Neos.Fusion.Form:Button>
            </div>

        </Neos.Fusion.Form:Form>

        <div class="neos-hide" id="deleteOverride">
            <div class="neos-modal">
                <div class="neos-modal-header">
                    <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                    <div class="neos-header">Delete override</div>
                </div>
                <div class="neos-modal-footer">
                    <a href="#" class="neos-button" data-dismiss="modal">Cancel</a>
                    <Neos.Fusion.Form:Form
                        form.target.action="deleteOverride"
                        form.data.translationOverride={translationOverride}
                        attributes.class="neos-inline"
                    >
                        <Neos.Fusion.Form:Hidden field.name="translationOverride[sourceIdentifier]" />
                        <Neos.Fusion.Form:Hidden field.name="translationOverride[labelIdentifier]" />
                        <Neos.Fusion.Form:Hidden field.name="translationOverride[localeIdentifier]" />

                        <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-danger">Delete</Neos.Fusion.Form:Button>
                    </Neos.Fusion.Form:Form>
                </div>
            </div>
            <div class="neos-modal-backdrop neos-in"></div>
        </div>
    `
}
